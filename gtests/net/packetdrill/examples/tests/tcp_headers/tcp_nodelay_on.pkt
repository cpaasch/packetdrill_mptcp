/* mptcp 0.88
 * A simple test using tcp_nodelay from initiator. 
 * Send 2 small packets (= smaller than mss) and ack each of them
 */

0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
0.000 bind(3, ..., ...) = 0
0.000 listen(3, 1) = 0

// Establish mptcp connection
0.100  < S 0:0(0) win 32792 <mss 1028,sackOK,nop,nop,nop,wscale 7,mp_capable a>
0.100  > S. 0:0(0) ack 1 win 28800 <mss 1460,sackOK,nop,nop,nop,wscale 7,mp_capable b>
0.200  < . 1:1(0) ack 1 win 257 <mp_capable a b, dss dack4=trunc_r64_hmac(b) + 1>
0.200  accept(3, ..., ...) = 4

// Set TCP_NODELAY enabled => Nagle algorithm is off
+0 setsockopt(4, SOL_TCP, TCP_NODELAY, [1], 4) = 0

// Send 2 data segment and get an ACK for each.
+0  write(4, ..., 4) = 4
+0  > P. 1:5(4) ack 1 win 450 <dss dack4=trunc_r64_hmac(a)+1 dsn4=trunc_r64_hmac(b)+1> sock(4)
+.1 < . 1:1(0) ack 5 win 257 <dss dack4=trunc_r64_hmac(b)+5> sock(4)

+0  write(4, ..., 1) = 1
+0  > P. 5:6(1) ack 1 win 450 <dss dack4=trunc_r64_hmac(a)+1 dsn4=trunc_r64_hmac(b)+5>  sock(4)
+.1 < . 5:5(0) ack 6 win 257  <dss dack4=trunc_r64_hmac(b)+6> sock(4)
