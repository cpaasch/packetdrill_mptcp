/*
 * tcp_cork_on_03.pkt
 *
 *  Created on: Jun 3, 2014
 *      Author: eduard
 *
 * mptcp v0.88
 * Test buffering several packets before sending it.
 * It's done using TCP_CORK flag.
 */

0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
0.000 bind(3, ..., ...) = 0
0.000 listen(3, 1) = 0

0.1 < S 0:0(0) win 32792 <mss 1020,sackOK,nop,nop,nop,wscale 7,mp_capable a>
0.1 > S. 0:0(0) ack 1 win 28800 <mss 1460,sackOK,nop,nop,nop,wscale 7,mp_capable b>
0.2 < . 1:1(0) ack 1 win 257 <mp_capable a b, dss dack4>
0.2 accept(3, ..., ...) = 4

// Put TCP_CORK flag ON
+0 setsockopt(4, SOL_TCP, TCP_CORK, [1], 4) = 0

// Send 1 entire packet
+0 write(4, ..., 800) = 800
+0 write(4, ..., 100) = 100

// The time is not respected. it's sent at +0.6 instead of +0.2
+0.6 > P. 1:901(900) ack 1 <dss dack4 dsn4>
+0 < . 1:1(0) ack 901 win 257 <dss dack4>

// overpass the MSS by 1 byte.
+0 write(4, ..., 50) = 50
+0 write(4, ..., 50) = 50
+0 write(4, ..., 901) = 901

+0 > . 901:1901(1000) ack 1 <dss dack4 dsn4>
+0.01 < . 1:1(0) ack 1901 win 257 <dss dack4>

// The time is not respected. it's sent at +0.58 instead of +0.2
+0.2 > P. 1901:1902(1) ack 1 <dss dack4 dsn4>
+0.01 < . 1:1(0) ack 1902 win 257 <dss dack4>

